* Astable Multivibrator
* Designed to be run from LTSPICE's scad3.exe.
* Invocation: scad3.exe -ascii -b multivibrator.net
* Output nodes O1 and O2
* 5V power supply on N0001
* Ground is 0
* Format:
* Label From To Value
* Params to be set in params.lib:
*  r1v, r2v, r3v, r4v, c1v, c2v
 
.INCLUDE "params.lib"

V1 N001 0 5
R1 N001 O1 {r1v}
R2 N001 N002 {r2v}
R3 N001 N003 {r3v}
R4 N001 O2 {r4v}
C1 N002 O1 {c1v}
C2 O2 N003 {c2v}
Q1 O1 N003 0 0 2N3904
Q2 O2 N002 0 0 2N3904

* This next bit is a definition for the 2N3904 NPN BJT
.model 2N3904 NPN(IS=1E-14 VAF=100
+  Bf=300 IKF=0.4 XTB=1.5 BR=4
+  CJC=4E-12  CJE=8E-12 RB=20 RC=0.1 RE=0.1
+  TR=250E-9  TF=350E-12 ITF=1 VTF=2 XTF=3 Vceo=40 Icrating=200m mfg=Philips)

* Do a transient simulation for four seconds
.tran 4 startup
* Save the output node's voltage to a text file
.save v(o1)
.end
